apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: '../dependencies.gradle'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion versions.TARGET_SDK
    buildToolsVersion versions.BUILD_TOOLS

    defaultConfig {
        minSdkVersion versions.MIN_SDK
        targetSdkVersion versions.TARGET_SDK
        versionCode versions.VERSION_CODE
        versionName versions.VERSION_NAME
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    resourcePrefix "substratum_"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.KOTLIN}"
    compile "com.android.support:appcompat-v7:${versions.ANDROID_SUPPORT_LIBS}"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
